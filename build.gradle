plugins {
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

subprojects {
	group = 'org.emartos'
	version = '0.0.1-SNAPSHOT'

	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'

	ext {
		findbugsVersion = '3.0.0'
		checkstyleVersion = '6.1.1'
		pmdVersion = '5.3.6'
	}
	repositories {
		jcenter()
		maven { url 'https://jitpack.io' }
	}

//	****FIND BUGS****//
	apply plugin: 'findbugs'
	findbugs {
		findbugsMain.enabled = true
		findbugsTest.enabled = true
		toolVersion = findbugsVersion
	}

	//human readable HTML output
	tasks.withType(FindBugs) {
		reports {
			xml.enabled = false
			html.enabled = true
		}
	}
    //****PMD****//
	apply plugin: 'pmd'
	pmd {
		toolVersion = pmdVersion
		pmdMain.enabled = true
		pmdTest.enabled= true
	}
	tasks.withType(Pmd){
		reports{
			xml.enabled=true
			html.enabled=true
		}
	}

	//****CHECKSTYLE****//
	apply plugin: 'checkstyle'
	def configDir = "${project.rootDir}/quality"
	checkstyle {
		toolVersion = checkstyleVersion
		configFile file("$configDir/checkstyle/checkstyle.xml")
		checkstyleMain.enabled = true
		checkstyleTest.enabled = true
	}

	tasks.withType(Checkstyle) {
		reports {
			xml.enabled = false
			html.enabled = true
		}
	}

	dependencyManagement {
		imports {
			//This allows us to specify version numbers in a single place
			// instead of spreading them over multiple modules
			mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.7.RELEASE")
		}
	}

	compileJava {
		sourceCompatibility = 1.8
	}

}
